plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

group = 'com.gitee.cbsd_mobile'

afterEvaluate {
    publishing {
        publications {
            mavenRelease(MavenPublication) {
                groupId = 'com.gitee.cbsd_mobile'
                artifactId = 'KtPermissions'
                version = '1.0.0'
                afterEvaluate {
                    from components.release
                }
            }
        }
        repositories {
            maven {
                url = keystoreProperties['MAVEN_URL']
                credentials {
                    username = keystoreProperties['username']
                    password = keystoreProperties['password']
                }
            }
        }
    }
}
android {
    namespace 'cn.zhaosunny.ktpermissions'

    compileSdk rootProject.ext.compileSdk
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdk rootProject.ext.minSdk
        targetSdk rootProject.ext.targetSdk
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

}

dependencies {
    api 'androidx.appcompat:appcompat:1.3.1'
    api 'androidx.fragment:fragment:1.3.1'
    api 'androidx.annotation:annotation:1.2.0'

    testImplementation 'junit:junit:4.13'

    api "androidx.core:core:1.7.0"
    api "androidx.core:core-ktx:1.7.0"
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"
}


